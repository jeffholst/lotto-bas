100 ' Texas Lotto Generator
200 VERSION$ = "0.0.9"
300 ' By: Jeff Holst and Gary Stevens
400 ' Date: May 10, 2019
500 ' Last Modified: December 1, 2021
600 '
700 ' Usefull commands
800 ' load "lotto.bas"
900 ' save "lotto.bas",a
1000 ' renun 100,100,100
1100 '
1200 ' Documentation: https://robhagemans.github.io/pcbasic/doc/2.0/
1300 '
1400 ' The following variables are the ones to set per lotto game
1500 '
1600 ' BALLCOUNT%          - Number of balls in game
1700 ' BALLMINNUMBER%      - Lowest ball number in game
1800 ' BALLMAXNUMBER%      - Highest ball number in game
1900 ' HASBONUSBALL%       - Is there a bonus ball? 0 = false, 1 = true
2000 ' BONUSBALLMINNUMBER% - Lowest bonus ball number
2100 ' BONUSBALLMAXNUMBER% - Allow duplicate numbers? 0 = false, 1 = true
2200 '
2300 ' ******** Variables below here probably do not need to be tweaked
2400 '
2500 DIM BALLS%(12)           ' Array of normal ball values
2600 DIM SORTEDBALLS%(12)     ' Array of sorted ball values
2700 DIM TEMPBALLS%(100)      ' Array of temp ball values
2800 BONUSBALL% = 0           ' Value of bonus ball
2900 ORDERNUMBERS% = 0        ' Order numbers? 0 = false, 1 = true
3000 GAMENAME$ = ""           ' Name of game
3100 FIRSTRUN% = 1            ' Display a random game on first run
3200 LOCATELINE% = 1          ' The line number to print centered text on
3300 CENTERLINE$ = ""         ' The text to print centered
3400 SCREENWIDTH% = 80        ' The character width of the screen
3500 LASTGAME% = 0            ' Last game number played
3600 CURRENTGAME% = 0         ' Current game number
3700 DIM BANNER$(6)           ' Game name banner array
3800 DISPLAY% = 1             ' 1 = SCREEN, 2 = PRINTER
3900 DRAWINGS% = 1            ' number of drawings to do
4000 CENTERTEXT% = 1          ' 1 = YES, 0 = NO
4100 '
4200 '
4300 RANDOMIZE TIMER
4400 '
4500 ' REMOVE MACROS FROM FUNCTION KEYS
4600 '
4700 FOR LOOP1% = 2 TO 10
4800 KEY LOOP1%, ""
4900 NEXT LOOP1%
5000 KEY 1, "1" ' SET F1 = 1
5100 KEY OFF ' HIDE SHORTCUT KEYS
5200 CLS
5300 GOSUB 25500 ' PRINT VERSION
5400 GOSUB 51000 ' MENU
5500 GOTO 54800 ' INPUT SECTION
5600 '
5700 ' **********************************************************
5800 IF LASTGAME% = 1 THEN GOTO 7700
5900 COLOR 9,0
6000 BANNER$(1) = " ___                    _          _ _ "
6100 BANNER$(2) = "| _ \_____ __ _____ _ _| |__  __ _| | |"
6200 BANNER$(3) = "|  _/ _ \ V  V / -_) '_| '_ \/ _` | | |"
6300 BANNER$(4) = "|_| \___/\_/\_/\___|_| |_.__/\__,_|_|_|"
6400 BANNER$(5) = ""
6500 BANNER$(6) = ""
6600 GOSUB 28100 ' PRINT BANNER
6700 GAMENAME$ = "Powerball"
6800 BALLCOUNT% = 5
6900 BALLMINNUMBER% = 1
7000 BALLMAXNUMBER% = 69
7100 HASBONUSBALL% = 1
7200 BONUSBALLMINNUMBER% = 1
7300 BONUSBALLMAXNUMBER% = 26
7400 ALLOWDUPES% = 0
7500 CURRENTGAME% = 1
7600 GOSUB 30000 ' PRINT GAME RULES
7700 GOSUB 41100 ' GENERATE AND PRINT BALLS
7800 LASTGAME% = 1
7900 RETURN
8000 '
8100 ' ***********************************************************
8200 IF LASTGAME% = 2 THEN GOTO 10100
8300 COLOR 9,0
8400 BANNER$(1) =  " __   _                 __  __ _ _ _ _             "
8500 BANNER$(2) =  "|  \/  |___ __ _ __ _  |  \/  (_) | (_)___ _ _  ___"
8600 BANNER$(3) =  "| |\/| / -_) _` / _` | | |\/| | | | | / _ \ ' \(_-<"
8700 BANNER$(4) =  "|_|  |_\___\__, \__,_| |_|  |_|_|_|_|_\___/_||_/__/"
8800 BANNER$(5) =  "           |___/                                   "
8900 BANNER$(6) =  ""
9000 GOSUB 28100 ' PRINT BANNER
9100 GAMENAME$ = "Mega Millions"
9200 BALLCOUNT% = 5
9300 BALLMINNUMBER% = 1
9400 BALLMAXNUMBER% = 70
9500 HASBONUSBALL% = 1
9600 BONUSBALLMINNUMBER% = 1
9700 BONUSBALLMAXNUMBER% = 25
9800 ALLOWDUPES% = 0
9900 CURRENTGAME% = 2
10000 GOSUB 30000 ' PRINT GAME RULES
10100 GOSUB 41100 ' GENERATE AND PRINT BALLS
10200 LASTGAME% = 2
10300 RETURN
10400 '
10500 ' ***********************************************************
10600 IF LASTGAME% = 3 THEN GOTO 12500
10700 COLOR 9,0
10800 BANNER$(1) = " _        _   _         _____                "            
10900 BANNER$(2) = "| |   ___| |_| |_ ___  |_   _|____ ____ _ ___"
11000 BANNER$(3) = "| |__/ _ \  _|  _/ _ \   | |/ -_) \ / _` (_-<"
11100 BANNER$(4) = "|____\___/\__|\__\___/   |_|\___/_\_\__,_/__/"
11200 BANNER$(5) = ""
11300 BANNER$(6) = ""
11400 GOSUB 28100 ' PRINT BANNER
11500 GAMENAME$ = "Lotto Texas"
11600 BALLCOUNT% = 6
11700 BALLMINNUMBER% = 1
11800 BALLMAXNUMBER% = 54
11900 HASBONUSBALL% = 0
12000 BONUSBALLMINNUMBER% = 0
12100 BONUSBALLMAXNUMBER% = 0
12200 ALLOWDUPES% = 0
12300 CURRENTGAME% = 3
12400 GOSUB 30000 ' PRINT GAME RULES
12500 GOSUB 41100 ' GENERATE AND PRINT BALLS
12600 LASTGAME% = 3
12700 RETURN
12800 '
12900 ' ***********************************************************
13000 IF LASTGAME% = 4 THEN GOTO 14900
13100 COLOR 9,0
13200 BANNER$(1) = " _____                  _____              ___ _            "      
13300 BANNER$(2) = "|_   _|____ ____ _ ___ |_   _|_ __ _____  / __| |_ ___ _ __ "
13400 BANNER$(3) = "  | |/ -_) \ / _` (_-<   | | \ V  V / _ \ \__ \  _/ -_) '_ \"
13500 BANNER$(4) = "  |_|\___/_\_\__,_/__/   |_|  \_/\_/\___/ |___/\__\___| .__/"
13600 BANNER$(5) = "                                                      |_|   "
13700 BANNER$(6) = ""
13800 GOSUB 28100 ' PRINT BANNER
13900 GAMENAME$ = "Texas Two Step"                
14000 BALLCOUNT% = 4
14100 BALLMINNUMBER% = 1
14200 BALLMAXNUMBER% = 35
14300 HASBONUSBALL% = 1
14400 BONUSBALLMINNUMBER% = 1
14500 BONUSBALLMAXNUMBER% = 35
14600 ALLOWDUPES% = 0
14700 CURRENTGAME% = 4
14800 GOSUB 30000 ' PRINT GAME RULES
14900 GOSUB 41100 ' GENERATE AND PRINT BALLS
15000 LASTGAME% = 4
15100 RETURN
15200 '
15300 ' ***********************************************************
15400 IF LASTGAME% = 5 THEN GOTO 17300
15500 COLOR 9,0
15600 BANNER$(1) = "   _   _ _             _  _     _   _    _           "        
15700 BANNER$(2) = "  /_\ | | |  ___ _ _  | \| |___| |_| |_ (_)_ _  __ _ "
15800 BANNER$(3) = " / _ \| | | / _ \ '_| | .` / _ \  _| ' \| | ' \/ _` |"
15900 BANNER$(4) = "/_/ \_\_|_| \___/_|   |_|\_\___/\__|_||_|_|_||_\__, |"
16000 BANNER$(5) = "                                               |___/ "
16100 BANNER$(6) = ""
16200 GOSUB 28100 ' PRINT BANNER
16300 GAMENAME$ = "All or Nothing"
16400 BALLCOUNT% = 12
16500 BALLMINNUMBER% = 1
16600 BALLMAXNUMBER% = 24
16700 HASBONUSBALL% = 0
16800 BONUSBALLMINNUMBER% = 0
16900 BONUSBALLMAXNUMBER% = 0
17000 ALLOWDUPES% = 0
17100 CURRENTGAME% = 5
17200 GOSUB 30000 ' PRINT GAME RULES
17300 GOSUB 41100 ' GENERATE AND PRINT BALLS
17400 LASTGAME% = 5
17500 RETURN
17600 '
17700 ' ***********************************************************
17800 IF LASTGAME% = 6 THEN GOTO 19800
17900 COLOR 9,0
18000 BANNER$(1) = " ___ _    _       ____"
18100 BANNER$(2) = "| _ (_)__| |__   |__ /"
18200 BANNER$(3) = "|  _/ / _| / /    |_ \"
18300 BANNER$(4) = "|_| |_\__|_\_\   |___/"
18400 BANNER$(5) = ""
18500 BANNER$(6) = ""
18600 GOSUB 28100 ' PRINT BANNER
18700 GAMENAME$ = "Pick 3"
18800 BALLCOUNT% = 3
18900 BALLMINNUMBER% = 0
19000 BALLMAXNUMBER% = 9
19100 HASBONUSBALL% = 0
19200 BONUSBALLMINNUMBER% = 0
19300 BONUSBALLMAXNUMBER% = 0
19400 ORDERNUMBERS% = 0
19500 ALLOWDUPES% = 1
19600 CURRENTGAME% = 6
19700 GOSUB 30000 ' PRINT GAME RULES
19800 GOSUB 41100 ' GENERATE AND PRINT BALLS
19900 LASTGAME% = 6
20000 RETURN       
20100 '
20200 ' ***********************************************************
20300 IF LASTGAME% = 7 THEN GOTO 22300
20400 COLOR 9,0
20500 BANNER$(1) = " ___       _ _         _ _  "
20600 BANNER$(2) = "|   \ __ _(_) |_  _   | | | "
20700 BANNER$(3) = "| |) / _` | | | || |  |_  _|"
20800 BANNER$(4) = "|___/\__,_|_|_|\_, |    |_| "
20900 BANNER$(5) = "               |__/         "
21000 BANNER$(6) = ""
21100 GOSUB 28100 ' PRINT BANNER
21200 GAMENAME$ = "Daily 4"
21300 BALLCOUNT% = 4
21400 BALLMINNUMBER% = 0
21500 BALLMAXNUMBER% = 9
21600 HASBONUSBALL% = 0
21700 BONUSBALLMINNUMBER% = 0
21800 BONUSBALLMAXNUMBER% = 0
21900 ORDERNUMBERS% = 0
22000 ALLOWDUPES% = 1
22100 CURRENTGAME% = 7
22200 GOSUB 30000 ' PRINT GAME RULES
22300 GOSUB 41100 ' GENERATE AND PRINT BALLS
22400 LASTGAME% = 7
22500 RETURN
22600 '
22700 ' ***********************************************************
22800 IF LASTGAME% = 8 THEN GOTO 24700
22900 COLOR 9,0
23000 BANNER$(1) = "  ___         _       ___ _         "  
23100 BANNER$(2) = " / __|__ _ __| |_    | __(_)_ _____ "
23200 BANNER$(3) = "| (__/ _` (_-< ' \   | _|| \ V / -_)"
23300 BANNER$(4) = " \___\__,_/__/_||_|  |_| |_|\_/\___|"
23400 BANNER$(5) = ""
23500 BANNER$(6) = ""
23600 GOSUB 28100 ' PRINT BANNER
23700 GAMENAME$ = "Cash Five"
23800 BALLCOUNT% = 5
23900 BALLMINNUMBER% = 1
24000 BALLMAXNUMBER% = 35
24100 HASBONUSBALL% = 0
24200 BONUSBALLMINNUMBER% = 0
24300 BONUSBALLMAXNUMBER% = 0
24400 ALLOWDUPES% = 0
24500 CURRENTGAME% = 8
24600 GOSUB 30000 ' PRINT GAME RULES
24700 GOSUB 41100 ' GENERATE AND PRINT BALLS
24800 LASTGAME% = 8
24900 RETURN
25000 '
25100 ' ***********************************************************
25200 '
25300 ' PRINT VERSION SUBROUTINE
25400 '
25500 COLOR 9,0
25600 LOCATELINE% = 1
25700 PRINT " VER " + VERSION$
25800 RETURN
25900 '
26000 ' ***********************************************************
26100 '
26200 ' PRINT TITLE SUBROUTINE
26300 '
26400 IF CENTERTEXT% = 1 THEN GOTO 27000
26500 IF DISPLAY% = 1 THEN GOTO 26800
26600 LPRINT TITLE$
26700 GOTO 27500
26800 PRINT TITLE$
26900 GOTO 27500
27000 LOCATELINE% = 9
27100 GOSUB 32800 ' CLEAR LINE
27200 COLOR 5,0
27300 CENTERLINE$ = TITLE$
27400 GOSUB 32000
27500 RETURN
27600 '
27700 ' ************************************************************
27800 '
27900 ' PRINT BANNER SUBROUTINE
28000 '
28100 FOR LOOP1% = 1 TO 6
28200 LOCATELINE% = LOOP1% + 2
28300 IF CENTERTEXT% = 1 THEN GOTO 29000
28400 IF DISPLAY% = 1 THEN GOTO 28700
28500 LPRINT BANNER$(LOOP1%)
28600 GOTO 29300
28700 LOCATE LOCATELINE%, 1
28800 PRINT BANNER$(LOOP1%)
28900 GOTO 29300
29000 GOSUB 32800 ' CLEAR LINE
29100 CENTERLINE$ = BANNER$(LOOP1%)
29200 GOSUB 32000 ' PRINT CENTERED LINE
29300 NEXT LOOP1%
29400 RETURN
29500 '
29600 ' ***********************************************************  
29700 '
29800 ' CREATE TITLE WITH GAME RULES
29900 '
30000 IF CURRENTGAME% = LASTGAME% THEN GOTO 45600                
30100 TEMP1$ = STR$(BALLMINNUMBER%)
30200 TEMP1$ = MID$(TEMP1$, 2, LEN(TEMP1$) - 1)
30300 TEMP2$ = STR$(BALLMAXNUMBER%)
30400 TEMP2$ = MID$(TEMP2$, 2, LEN(TEMP2$) - 1)
30500 TITLE$ = " " + GAMENAME$ + " :" + STR$(BALLCOUNT%) + "  " + TEMP1$ + "-" + TEMP2$ + " "
30600 IF HASBONUSBALL% = 1 THEN GOTO 30800
30700 GOTO 31300
30800 TEMP1$ = STR$(BONUSBALLMINNUMBER%)
30900 TEMP1$ = MID$(TEMP1$, 2, LEN(TEMP1$) - 1)
31000 TEMP2$ = STR$(BONUSBALLMAXNUMBER%)
31100 TEMP2$ = MID$(TEMP2$, 2, LEN(TEMP2$) - 1)
31200 TITLE$ = TITLE$ + " and" + STR$(BONUSBALLMINNUMBER%) + "  " + TEMP1$ + "-" + TEMP2$ + " "
31300 GOSUB 26400 ' PRINT TITLE
31400 RETURN
31500 '
31600 ' ***********************************************************  
31700 '
31800 ' PRINT CENTERED LINE SUBROUTINE
31900 '
32000 LOCATE LOCATELINE%, (SCREENWIDTH% / 2) - (LEN(CENTERLINE$) / 2)
32100 PRINT CENTERLINE$
32200 RETURN
32300 '
32400 ' ***********************************************************  
32500 '
32600 ' CLEAR LINE SUBROUTINE
32700 '
32800 FOR LOOP2% = 1 TO SCREENWIDTH%
32900 LOCATE LOCATELINE%, LOOP2%
33000 PRINT " "
33100 NEXT LOOP2%
33200 RETURN
33300 '
33400 ' ***********************************************************  
33500 '
33600 ' PRINT BALLS CENTERED SUBROUTINE
33700 '
33800 COLOR 12,0
33900 IF HASBONUSBALL% = 0 THEN CENTERLINE$ = OUTPUT$: GOTO 32000 ' RETURN in Subroutine
34000 BONUSBALLLABEL$ = " Bonus Ball:"
34100 MYLEN% = LEN(OUTPUT$) + LEN(BONUSBALLLABEL$) + LEN(STR$(BONUSBALL%))
34200 STARTPOSITION% = (SCREENWIDTH% / 2) - (MYLEN%  / 2)
34300 LOCATE LOCATELINE%, STARTPOSITION%
34400 PRINT OUTPUT$
34500 LOCATE LOCATELINE%, STARTPOSITION% + LEN(OUTPUT$)
34600 COLOR 11,0
34700 PRINT BONUSBALLLABEL$
34800 COLOR 12,0
34900 LOCATE LOCATELINE%, STARTPOSITION% + LEN(OUTPUT$) + LEN(BONUSBALLLABEL$)
35000 PRINT STR$(BONUSBALL%)
35100 RETURN
35200 '
35300 ' ***********************************************************
35400 '
35500 ' PRINTER SUBROUTINE
35600 '
35700 FOR LOOP1% = 1 TO 6
35800 LPRINT BANNER$(LOOP1%)
35900 NEXT LOOP1%
36000 OUTPUT$ = ""
36100 ADDCOMMA% = 0
36200 FOR LOOP1% = 0 TO BALLCOUNT% - 1
36300 IF ADDCOMMA% = 1 THEN OUTPUT$ = OUTPUT$ + ","
36400 IF ORDERNUMBERS% = 0 THEN MYOUT$ = STR$(BALLS%(LOOP1%)) ELSE MYOUT$ = STR$(SORTEDBALLS%(LOOP1%))
36500 OUTPUT$ = OUTPUT$ + MYOUT$
36600 ADDCOMMA% = 1
36700 NEXT LOOP1%
36800 IF HASBONUSBALL% = 0 THEN GOTO 37000
36900 OUTPUT$ = OUTPUT$ + " Bonus Ball:" + STR$(BONUSBALL%)
37000 LPRINT OUTPUT$
37100 GOTO 54900
37200 '
37300 ' ***********************************************************  
37400 '
37500 ' Print Multiple Drawings
37600 '
37700 CLS
37800 CENTERTEXT% = 0
37900 LASTGAME% = 0
38000 INPUT "How many drawings "; DRAWINGS%
38100 PRINT "1 = Screen, 2 = Printer"
38200 IN$ = INKEY$
38300 IF IN$ = "" THEN 38200
38400 IF IN$ = "1" THEN DISPLAY% =1: GOTO 38700
38500 IF IN$ = "2" THEN DISPLAY% = 2: GOTO 38700
38600 GOTO 38200
38700 IF CURRENTGAME% = 1 THEN GOSUB 5900
38800 IF CURRENTGAME% = 2 THEN GOSUB 8300
38900 IF CURRENTGAME% = 3 THEN GOSUB 10700
39000 IF CURRENTGAME% = 4 THEN GOSUB 13100
39100 IF CURRENTGAME% = 5 THEN GOSUB 15500
39200 IF CURRENTGAME% = 6 THEN GOSUB 17900
39300 IF CURRENTGAME% = 7 THEN GOSUB 20400
39400 IF CURRENTGAME% = 8 THEN GOSUB 22900
39500 FIRSTRUN% = 1
39600 DRAWINGS% = 1
39700 DISPLAY% = 1
39800 PRINT ""
39900 PRINT "Hit any key to continue"
40000 IN$ = INKEY$
40100 IF IN$ = "" THEN 40000
40200 CLS
40300 FIRSTRUN% = 1
40400 CENTERTEXT% = 1
40500 GOTO 5200
40600 '
40700 ' ***********************************************************  
40800 '
40900 ' GAME LOOP
41000 '
41100 FOR GAMECOUNT% = 1 TO DRAWINGS%
41200 GOSUB 41800
41300 NEXT GAMECOUNT%
41400 RETURN
41500 '
41600 ' GENERATE BALLS
41700 '
41800 IF ALLOWDUPES% = 0 THEN GOTO 43100
41900 '
42000 ' DUPLICATE BALLS OKAY
42100 '
42200 FOR LOOP1% = 0 TO BALLCOUNT% - 1
42300 RBALL% = INT(RND * (BALLMAXNUMBER% + 1)) + BALLMINNUMBER%
42400 BALLS%(LOOP1%) = RBALL%
42500 SORTEDBALLS%(LOOP1%) = RBALL%
42600 NEXT LOOP1%
42700 GOTO 45600
42800 '
42900 ' NO DUPLICATE BALLS ALLOWED
43000 '
43100 COUNTER% = BALLMINNUMBER%
43200 FOR LOOP1% = 0 TO BALLMAXNUMBER% - BALLMINNUMBER%
43300 TEMPBALLS%(LOOP1%) = COUNTER%
43400 COUNTER% = COUNTER% + 1
43500 NEXT LOOP1%
43600 '
43700 ' RANDOMIZE TEMP BALL ARRAY
43800 '
43900 FOR LOOP1% = 0 TO BALLMAXNUMBER% - BALLMINNUMBER%
44000 TEMP% = TEMPBALLS%(LOOP1%)
44100 INDEX% = INT(RND * BALLMAXNUMBER% - BALLMINNUMBER% + 1)
44200 '
44300 TEMPBALLS%(LOOP1%) = TEMPBALLS%(INDEX%)
44400 TEMPBALLS%(INDEX%) = TEMP%
44500 NEXT LOOP1%
44600 '
44700 ' PUT RANDOMIZED BALLS IN BALL ARRAY
44800 '
44900 FOR LOOP1% = 0 TO BALLCOUNT% - 1
45000 BALLS%(LOOP1%) = TEMPBALLS%(LOOP1%)
45100 SORTEDBALLS%(LOOP1%) = TEMPBALLS%(LOOP1%)
45200 NEXT LOOP1%
45300 '
45400 ' ***********************************************************  
45500 '
45600 IF HASBONUSBALL% = 0 THEN GOTO 46800
45700 '
45800 ' ***********************************************************  
45900 '
46000 ' SELECT BONUS BALL
46100 '
46200 BONUSBALL% = INT(RND * BONUSBALLMAXNUMBER%) + BONUSBALLMINNUMBER%
46300 '
46400 ' ***********************************************************
46500 '
46600 ' BUBBLE SORT
46700 '
46800 FOR LOOP1% = 0 TO BALLCOUNT% - 2
46900 FOR LOOP2% = 0 TO BALLCOUNT% - 2
47000 IF SORTEDBALLS%(LOOP2%) > SORTEDBALLS%(LOOP2%  + 1) THEN GOSUB 47400
47100 NEXT LOOP2%
47200 NEXT LOOP1%
47300 GOTO 48300
47400 TEMP% = SORTEDBALLS%(LOOP2%)
47500 SORTEDBALLS%(LOOP2%) = SORTEDBALLS%(LOOP2% + 1)
47600 SORTEDBALLS%(LOOP2% + 1) = TEMP%
47700 RETURN
47800 '
47900 ' ***********************************************************
48000 '
48100 ' PRINT BALLS SUBROUTINE
48200 '
48300 OUTPUT$ = ""
48400 ADDCOMMA% = 0
48500 FOR LOOP1% = 0 TO BALLCOUNT% - 1
48600 IF ADDCOMMA% = 1 THEN OUTPUT$ = OUTPUT$ + ","
48700 IF ORDERNUMBERS% = 0 THEN MYOUT$ = STR$(BALLS%(LOOP1%)) ELSE MYOUT$ = STR$(SORTEDBALLS%(LOOP1%))
48800 OUTPUT$ = OUTPUT$ + MYOUT$
48900 ADDCOMMA% = 1
49000 NEXT LOOP1%
49100 IF CENTERTEXT% = 1 THEN GOTO 49900
49200 OUTPUT$ = STR$(GAMECOUNT%) + ") " + OUTPUT$
49300 IF HASBONUSBALL% = 1 THEN OUTPUT$ = OUTPUT$ + " Bonus Ball:" + STR$(BONUSBALL%)
49400 IF DISPLAY% = 1 THEN GOTO 49700
49500 LPRINT OUTPUT$
49600 GOTO 50200
49700 PRINT OUTPUT$
49800 GOTO 50200
49900 LOCATELINE% = 11
50000 GOSUB 32800 ' CLEAR LINE
50100 GOSUB 33800 ' CENTER BALLS
50200 IF ORDERNUMBERS% <> LASTORDERNUMBERS% THEN GOSUB 51000
50300 RETURN
50400 '
50500 ' ***********************************************************
50600 '  
50700 ' MAIN MENU SUBROUTINE
50800 '
50900 '
51000 LOCATE 15,10
51100 COLOR 2,0
51200 PRINT "(Q)uit, "
51300 IF ORDERNUMBERS% = 1 THEN LASTORDERNUMBERS% = 1: COLOR 14, 0
51400 LOCATE 15, 18
51500 PRINT "in (O)rder"
51600 COLOR 2, 0
51700 LOCATE 15, 28
51800 PRINT ", "
51900 IF ORDERNUMBERS% = 0 THEN LASTORDERNUMBERS% = 0: COLOR 14, 0
52000 LOCATE 15, 30
52100 PRINT "(U)nsorted"
52200 COLOR 2, 0
52300 LOCATE 15,40
52400 PRINT ", (P)owerball, (M)ega Millions"
52500 LOCATELINE%  = 17
52600 CENTERLINE$ = "(L)otto Texas, (C)ash Five, Pick (3), (D)aily 4"
52700 GOSUB 32000
52800 LOCATELINE% = 19
52900 CENTERLINE$ = "(A)ll or Nothing, (T)exas Two Step, (R)andom"
53000 GOSUB 32000
53100 LOCATELINE% = 21
53200 CENTERLINE$ = "1 = Print Multiple Drawings, 2 = Print Current Screen"
53300 GOSUB 32000
53400 RETURN
53500 '
53600 ' ***********************************************************
53700 '  
53800 ' CHECK IF SORT ALLOWED
53900 '
54000 IF LASTGAME% = 6 OR LASTGAME% = 7 THEN BEEP: RETURN ' SORT NOT ALLOWED FOR THESE GAMES
54100 ORDERNUMBERS% = 1
54200 GOSUB 51000: GOSUB 48300
54300 '
54400 ' ***********************************************************
54500 '  
54600 ' INPUT SECTION
54700 '
54800 IF FIRSTRUN% = 1 THEN GOTO 57200    
54900 COLOR 9, 0
55000 LOCATELINE% = 23
55100 CENTERLINE$ = DATE$ + "  " + TIME$
55200 GOSUB 32000 ' PRINT CENTERLINE$
55300 DRAWINGS% = 1
55400 CENTERTEXT% = 1
55500 IN$ = INKEY$
55600 IF IN$ = "" THEN 54900
55700 IF IN$ = "q" OR IN$ = "Q" THEN GOTO 58300
55800 IF IN$ = "o" OR IN$ = "O" THEN GOSUB 54000
55900 IF IN$ = "u" OR IN$ = "U" THEN ORDERNUMBERS% = 0: GOSUB 51000: GOSUB 48300
56000 IF IN$ = "p" OR IN$ = "P" THEN GOSUB 5800: GOTO 54300     ' Powerball
56100 IF IN$ = "m" OR IN$ = "M" THEN GOSUB 8200: GOTO 54300     ' Mega Millions
56200 IF IN$ = "l" OR IN$ = "L" THEN GOSUB 10600: GOTO 54300     ' Lotto Texas
56300 IF IN$ = "c" OR IN$ = "C" THEN GOSUB 22800: GOTO 54300    ' Cash Five
56400 IF IN$ = "d" OR IN$ = "D" THEN GOSUB 20300: GOTO 54300    ' Daily 4
56500 IF IN$ = "a" OR IN$ = "A" THEN GOSUB 15400: GOTO 54300    ' All or Nothing
56600 IF IN$ = "t" OR IN$ = "T" THEN GOSUB 13000: GOTO 54300    ' Texas Two Step
56700 IF IN$ = "r" OR IN$ = "R" THEN GOSUB 57300: GOTO 54300    ' Random
56800 IF IN$ = "3" THEN GOSUB 17800: GOTO 54300                 ' Pick 3
56900 IF IN$ = "2" THEN GOTO 35200
57000 IF IN$ = "1" THEN GOTO 37200
57100 GOTO 54900
57200 FIRSTRUN% = 0
57300 GAME% = INT(RND * 8) + 1
57400 IF GAME% = 1 THEN GOSUB 5800
57500 IF GAME% = 2 THEN GOSUB 8200
57600 IF GAME% = 3 THEN GOSUB 10600
57700 IF GAME% = 4 THEN GOSUB 22800
57800 IF GAME% = 5 THEN GOSUB 17800
57900 IF GAME% = 6 THEN GOSUB 20300
58000 IF GAME% = 7 THEN GOSUB 15400
58100 IF GAME% = 8 THEN GOSUB 13000
58200 GOTO 54900
58300 '
58400 ' EXIT PROGRAM AND SET THINGS BACK TO DEFAULT
58500 SYSTEM : END '
58600 CLS
58700 COLOR 7,0
58800 KEY 1, "LIST"
58900 KEY 2, "RUN" + CHR$(&HD)
59000 KEY 3, "LOAD" + CHR$(&H22)
59100 KEY 4, "SAVE" + CHR$(&H22)
59200 KEY 5, "CONT" + CHR$(&HD)
59300 KEY 6, "," + CHR$(&H22) + "LPT1:" + CHR$(&H22) + CHR$(&HD)
59400 KEY 7, "TRON" + CHR$(&HD)
59500 KEY 8, "TROFF" + CHR$(&HD)
59600 KEY 9, "KEY "
59700 KEY 10, "SCREEN 0,0,0" + CHR$(&HD)
59800 KEY ON
59900 SYSTEM : END : SYSTEM

